plugins {
    id 'net.ltgt.nullaway' apply false
    id 'net.ltgt.errorprone' apply false
    id 'com.diffplug.gradle.spotless' apply false
    id 'com.palantir.baseline' apply false
}

subprojects { project ->
    group 'io.github.joke.caffeinate'

    repositories {
        mavenCentral()
    }

    pluginManager.apply 'com.palantir.baseline-reproducibility'

    pluginManager.withPlugin('java-base') {
        pluginManager.apply 'com.palantir.baseline-idea'
        pluginManager.apply 'com.palantir.baseline-encoding'
        pluginManager.apply 'com.palantir.baseline-java-compiler-diagnostics'
        pluginManager.apply 'com.palantir.baseline-java-properties'
        pluginManager.apply 'com.diffplug.spotless'
        // pluginManager.apply 'com.palantir.baseline-format'
        tasks.withType(JavaCompile).configureEach {
            options.release = 8
            options.compilerArgs << '-Werror'
        }
    }

    pluginManager.withPlugin('com.diffplug.spotless') {
        spotless {
            java {
                forbidWildcardImports()
                removeUnusedImports()
                palantirJavaFormat('2.87.0')
            }
        }
    }

    pluginManager.withPlugin('java') {
        pluginManager.apply 'net.ltgt.errorprone'
        pluginManager.apply 'com.palantir.baseline-testing'
    }

    pluginManager.withPlugin('net.ltgt.errorprone') {
        pluginManager.apply 'net.ltgt.nullaway'
        dependencies {
            errorprone('com.google.errorprone:error_prone_core:2.47.0')
        }
        tasks.withType(JavaCompile).configureEach {
            options.errorprone {
                disableWarningsInGeneratedCode = true
            }
        }
    }

    pluginManager.withPlugin('net.ltgt.nullaway') {
        dependencies {
            errorprone('com.uber.nullaway:nullaway:0.13.1')
        }
        tasks.withType(JavaCompile).configureEach {
            options.errorprone {
                error('RequireExplicitNullMarking')
                nullaway {
                    error()
                    onlyNullMarked = true
                    treatGeneratedAsUnannotated = true
                    jspecifyMode = true
                    suggestSuppressions = true
                    checkContracts = true
                    acknowledgeRestrictiveAnnotations = true
                }
            }
        }
    }
}

wrapper {
    gradleVersion = '9.3.1'
}
